# Maintainer: onefire <onefire.myself@gmail.com>
pkgname=openblas-cblas
_pkgname=OpenBLAS
pkgver=0.2.8
pkgrel=3
pkgdesc="C interface to OpenBLAS, an optimized BLAS library based on GotoBLAS2 1.13 BSD, built with the Intel compilers."
arch=('x86_64')
url="http://xianyi.github.com/OpenBLAS/"
license=('BSD')
depends=('gcc-libs')
makedepends=('perl')
provides=('cblas=3.4.2')
conflicts=('cblas')
options=(!makeflags)
source=(${_pkgname}-v${pkgver}.tar.gz::http://github.com/xianyi/OpenBLAS/archive/v${pkgver}.tar.gz)
sha512sums=('35ac8d78e57950ba931d99317bc6b2cb4f460ff7882e456c9a13bb1ab7f42d12f3158ffd4ef7c46d2ee523b5c4abf204c4056a1d8f1954da50d176ffaaac5525')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  unset CFLAGS
  unset CXXFLAGS
  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=`grep "cores" /proc/cpuinfo|sort|tail -n 1|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC
  make CC=icc FC=ifort USE_OPENMP=1 ONLY_CBLAS=1 BINARY=64 \
    NUM_THREADS=$NCORE LIBPREFIX=libcblas NO_LAPACK=1 MAJOR_VERSION=3 -j 16
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  NPROC=`grep "physical id" /proc/cpuinfo|sort|uniq|wc -l`
  NCORE4PROC=` grep "cores" /proc/cpuinfo|sort|tail -n 1|sed -e 's/cpu cores.*: //'`
  let NCORE=NPROC*NCORE4PROC
  make PREFIX="$pkgdir/usr" NUM_THREADS=$NCORE LIBPREFIX=libcblas \
    MAJOR_VERSION=3 install
  
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd "$pkgdir/usr/lib/"
  ln -sf libcblas_*-r$pkgver.a libcblas.a
  ln -sf libcblas_*-r$pkgver.so libcblas.so
  ln -sf libcblas_*-r$pkgver.so libcblas.so.3
}
