# Maintainer: onefire <onefire.myself@gmail.com>
pkgname=nvidia-custom
pkgver=331.38
pkgrel=1
pkgdesc="NVIDIA drivers for linux. Customized to be compatible with libgl. Use this coupled with my other "custom" packages to have CUDA without bumblebee on laptops."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("linux>=3.12" "linux<3.13" "nvidia-utils-custom=${pkgver}")
makedepends=("linux-headers>=3.12" "linux-headers<3.13")
conflicts=('nvidia' 'nvidia-bumblebee')
provides=('nvidia')
license=('custom')
install=nvidia-custom.install
options=(!strip)
groups=('nvidia-cuda' 'custom')
_kernver=`uname -r`
_extramodules="extramodules-3.12-ARCH"

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	sha512sums=("ae071d404a07754fd3eb4102b0d5d4cf2ec92aecfe65b5f618650f477f78dfa7d776f6786d0b53df2e0f5f00a8fae330bbfbe3de24afbf50ebffc7b45b027e2d")

elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	sha512sums=("470b1c347dc82ed596b333401eb938c156863dd9c4a506b0f776713203fd403ada0900fe62a95f95adab12278e9b01cc3dc368fddd844c5737352f8d368acc28")
fi

source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

build() {
  cd "${srcdir}"
  sh "${_pkg}.run" --extract-only
  cd "${_pkg}/kernel"
  make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-custom.install"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
