# Maintainer: onefire <onefire.myself@gmail.com>
pkgname=nvidia-custom
pkgver=325.08
pkgrel=1
pkgdesc="NVIDIA drivers for linux. Customized to be compatible with libgl. Use this coupled with my other "custom" packages to have CUDA without bumblebee on laptops."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("linux>=3.9" "linux<3.10" "nvidia-utils-custom=${pkgver}")
makedepends=("linux-headers>=3.9" "linux-headers<3.10")
conflicts=('nvidia' 'nvidia-bumblebee')
provides=('nvidia')
license=('custom')
install=nvidia-custom.install
options=(!strip)
groups=('nvidia-cuda' 'custom')
_kernver=`uname -r`
_extramodules="extramodules-`echo $_kernver | head -c 3`-ARCH"

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	sha512sums=("c356fdc40d6dcca4e2f7f6387dbe7846405344eaae7f0bc2894a53940dc1d9893e6f245a496e4b328a6e7c476619597cc4535626eb350cfed491090ce35c36fd")

elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	sha512sums=("7c3e3f90046f414a8ddd448b4a0eaa69dc63917bb6609e141b21464ea814ecc297671942c00774ad6f57d7ba34f66cc7af56308414adbfce92824a624330c13d")
fi

source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

build() {
  cd "${srcdir}"
  sh "${_pkg}.run" --extract-only
  cd "${_pkg}/kernel"
  make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-custom.install"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
