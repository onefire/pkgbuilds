# Maintainer: onefire <onefire.myself@gmail.com>
pkgname=nvidia-custom
pkgver=331.13
pkgrel=1
pkgdesc="NVIDIA drivers for linux. Customized to be compatible with libgl. Use this coupled with my other "custom" packages to have CUDA without bumblebee on laptops."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=("linux>=3.11" "linux<3.12" "nvidia-utils-custom=${pkgver}")
makedepends=("linux-headers>=3.11" "linux-headers<3.12")
conflicts=('nvidia' 'nvidia-bumblebee')
provides=('nvidia')
license=('custom')
install=nvidia-custom.install
options=(!strip)
groups=('nvidia-cuda' 'custom')
_kernver=`uname -r`
_extramodules="extramodules-3.11-ARCH"

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	sha512sums=("97603a193e63dd747a38751e79dfb97425af2c9332d2a40bd0083ad82b35e003e602ba029d5c7b0683bbc4644af19e133214c72e72af0f23bf55976e61637151")

elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	sha512sums=("76456cc7a08aede5580814d81784e2f59d5aa9c0f32a71a86b8d4e1bc3b0d1d78a8ab29727854c3d592ffcf52c212266fdbfab2d48494115e265546a2a11afca")
fi

source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run" \
        "nvidia-linux-3.11.patch")

sha512sums+=("c1959dd96168901fccfd422cd308fed35874e8cb2a4e7ff70247f01b02f5cf33090bd24df8a129c34f25c17a67c0b22e5f1f1d189c9385e6594e68416f408900")

build() {
  cd "${srcdir}"
  sh "${_pkg}.run" --extract-only
  cd "${_pkg}/kernel"
  patch -Np2 -i ${srcdir}/nvidia-linux-3.11.patch
  make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-custom.install"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
