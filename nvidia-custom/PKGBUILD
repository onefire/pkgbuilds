# Maintainer: onefire <onefire.myself@gmail.com>
pkgname=nvidia-custom
pkgver=304.43
_extramodules=extramodules-3.4-ARCH
pkgrel=1
pkgdesc="NVIDIA drivers for linux. Customized to be compatible with libgl. Use this coupled with my other "custom" packages to have CUDA without bumblebee on laptops."
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
depends=('linux>=3.4' 'linux<3.5' "nvidia-utils-custom=${pkgver}")
makedepends=('linux-headers>=3.4' 'linux-headers<3.5')
conflicts=('nvidia' 'nvidia-bumblebee')
provides=('nvidia')
license=('custom')
install=nvidia-custom.install
options=(!strip)
groups=('nvidia-cuda' 'custom')

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	sha256sums=('7c7f483302a0a5a828b8ef1f5a4a53f8e16d3b73d0c17bd61ed0d843cad32c4c')

elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	sha256sums=('31b911f5bbd7c3ca3f1320f7f5fa60f193601f4af333f3322ee0987721edcd54')
fi

source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "${srcdir}"
  sh "${_pkg}.run" --extract-only
  cd "${_pkg}/kernel"
  make SYSSRC=/usr/lib/modules/"${_kernver}/build" module
}

package() {
  install -D -m644 "${srcdir}/${_pkg}/kernel/nvidia.ko" "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
  install -d -m755 "${pkgdir}/usr/lib/modprobe.d"
  echo "blacklist nouveau" >> "${pkgdir}/usr/lib/modprobe.d/nvidia.conf"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "${startdir}/nvidia-custom.install"
  gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
