# Maintainer: onefire <onefire.myself@gmail.com>
pkgname=('opencl-nvidia-custom')
pkgver=313.09
pkgrel=1
pkgdesc="OpenCL implemention for NVIDIA. Customized to be compatible with libgl. This and my package nvidia-utils-custom are broken down in two packages because the AUR does not support multiple packages per PKGBUILD."
depends=('libcl' 'zlib' "nvidia-utils")
optdepends=('opencl-headers: headers necessary for OpenCL development')
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')
provides=("opencl-nvidia=$pkgver")
conflicts=()
groups=('nvidia-cuda' 'custom')

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	sha256sums=("a253510ac2eabe85626d0b3d9123fdb14282919da8da0a265210bb852bf1078f")

elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	sha256sums=("f3ebc358d6d8e69a1e247bddc95ca7cc95cd39cdc3b1aa919817a2f4d2f97a91")
fi

source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

create_links() {
  # create soname links
  while read -d '' _lib; do
    _soname="$(dirname "${_lib}")/$(readelf -d "${_lib}" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
    [[ -e "${_soname}" ]] || ln -s "$(basename "${_lib}")" "${_soname}"
    [[ -e "${_soname/.[0-9]*/}" ]] || ln -s "$(basename "${_soname}")" "${_soname/.[0-9]*/}"
  done < <(find "${pkgdir}" -type f -name '*.so*' -print0)
}

build() {
  cd "${srcdir}"
  sh "$_pkg.run" --extract-only
}

package() {
  cd "${srcdir}/$_pkg"

  # OpenCL
  install -D -m644 nvidia.icd "${pkgdir}/etc/OpenCL/vendors/nvidia.icd"
  install -D -m755 "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-compiler.so.${pkgver}"
 
  create_links
}


