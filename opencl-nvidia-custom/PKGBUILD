# Maintainer: onefire <onefire.myself@gmail.com>
pkgname=('opencl-nvidia-custom')
pkgver=325.08
pkgrel=1
pkgdesc="OpenCL implemention for NVIDIA. Customized to be compatible with libgl. This and my package nvidia-utils-custom are broken down in two packages because the AUR does not support multiple packages per PKGBUILD."
depends=('libcl' 'zlib' "nvidia-utils")
optdepends=('opencl-headers: headers necessary for OpenCL development')
arch=('i686' 'x86_64')
url="http://www.nvidia.com/"
license=('custom')
options=('!strip')
provides=("opencl-nvidia=$pkgver")
conflicts=('opencl-nvidia')
groups=('nvidia-cuda' 'custom')

if [ "$CARCH" = "i686" ]; then
	_arch='x86'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}"
	sha512sums=("c356fdc40d6dcca4e2f7f6387dbe7846405344eaae7f0bc2894a53940dc1d9893e6f245a496e4b328a6e7c476619597cc4535626eb350cfed491090ce35c36fd")

elif [ "$CARCH" = "x86_64" ]; then
	_arch='x86_64'
	_pkg="NVIDIA-Linux-${_arch}-${pkgver}-no-compat32"
	sha512sums=("7c3e3f90046f414a8ddd448b4a0eaa69dc63917bb6609e141b21464ea814ecc297671942c00774ad6f57d7ba34f66cc7af56308414adbfce92824a624330c13d")
fi

source=("ftp://download.nvidia.com/XFree86/Linux-${_arch}/${pkgver}/${_pkg}.run")

create_links() {
  # create soname links
  while read -d '' _lib; do
    _soname="$(dirname "${_lib}")/$(readelf -d "${_lib}" | sed -nr 's/.*Library soname: \[(.*)\].*/\1/p')"
    [[ -e "${_soname}" ]] || ln -s "$(basename "${_lib}")" "${_soname}"
    [[ -e "${_soname/.[0-9]*/}" ]] || ln -s "$(basename "${_soname}")" "${_soname/.[0-9]*/}"
  done < <(find "${pkgdir}" -type f -name '*.so*' -print0)
}

build() {
  cd "${srcdir}"
  sh "$_pkg.run" --extract-only
}

package() {
  cd "${srcdir}/$_pkg"

  install -D -m644 nvidia.icd "${pkgdir}/etc/OpenCL/vendors/nvidia.icd"
  install -D -m755 "libnvidia-opencl.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-opencl.so.${pkgver}"
  install -D -m755 "libnvidia-compiler.so.${pkgver}" "${pkgdir}/usr/lib/libnvidia-compiler.so.${pkgver}"
 
  create_links
}


